plugins {
	id "architectury-plugin" version "3.0.93"
	id "forgified-fabric-loom" version "0.6.72" apply false
	id "maven-publish"
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "forgified-fabric-loom"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		// The following line declares the mojmap mappings, you may use other mappings as well
		// mappings loom.officialMojangMappings()
		// The following line declares the yarn mappings you may select this one as well.
		mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}"
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	archivesBaseName = rootProject.archives_base_name
	version = rootProject.mod_version
	group = rootProject.maven_group

	repositories {
		maven { url "https://maven.shedaniel.me" }
		maven { url "https://dl.bintray.com/ladysnake/libs" }
		maven { url "https://dl.bintray.com/adriantodt/maven" }
		maven { url "https://jitpack.io" }
		maven { url "https://maven.jamieswhiteshirt.com/libs-release/" }
		jcenter()
		mavenLocal()
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"

		// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
		// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
		// We'll use that if it's available, but otherwise we'll use the older option.
		def targetVersion = 8
		if (JavaVersion.current().isJava9Compatible()) {
			options.release = targetVersion
		}
	}

	java {
		withSourcesJar()
	}
}